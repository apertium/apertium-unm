# Morphological Transducer for Unami

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <adj>    # Adjective

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Number morphology
# <pl>  # Plural
# <sg>  # Singular

# Animacy
# <aa>  # Animate
# <nn>  # Inanimate

# Other symbols
# >      # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS

NounRoot-Anim NounInfl-Anim
NounRoot-Inan NounInfl-Inan

NounRoot-Family(1) [ <n><voc>: ]

N-Poss(1) NounRoot-Family(2) [ <n>: ] N-Poss(2) N-Poss(3): [ <pl>:{A}k ]?
# Just put a -k after "our", e.g, nichanenak

# Intransitive
:VerbInflAI(1) Preverbs? VerbRootAI(1) [<v>:] VerbRootAI(2): :VerbInflAI(2) VerbInflAI(3):
# Transitive Inanimate
:VerbInflTI(1) Preverbs? VerbRootTI(1) [<v>:] VerbRootTI(2): :VerbInflTI(2) VerbInflTI(3):
# Transitive Animate
:VerbInflTA(1) Preverbs? VerbRootTA(1) [<v>:] VerbRootTA(2): :VerbInflTA(2) VerbInflTA(3):

Punctuation

AdjRoot

AdvRoot

PrepRoot

IjRoot

DetRoot

NumberRoot

CnjLex(1) CnjLex(2):


PATTERN AdjRoot

AdjLex [<adj>:] AdjMorph
AdjLex-Cooking [<adj>:] AdjMorph
AdjLex-Cooking [<adj><??>:sasik]  # ! FIXME: what are these forms?

PATTERN AdvRoot

AdvLex [<adv>:]

PATTERN PrepRoot

Prepositions [<pr>:]

PATTERN IjRoot

IjLex [<ij>:]

PATTERN NumberRoot

NumberLex [<num>:]



LEXICON AdjMorph

<aa><pred><sg>:>su
<aa><pred><pl>:>su>{y}{A}k

<nn><pred><sg>:>e
<nn><pred><pl>:>e>{y}{A}

<aa><attr><sg>:>sit
<aa><attr><pl>:>sit>chik

<nn><attr><sg>:>e
<nn><attr><sg>:>ek        # Dir/LR
<nn><attr><pl>:>e>{y}{A}
<nn><attr><pl>:>e>{y}{A}k # Dir/LR  FIXME: CHECK




PATTERN Preverbs

Preverb-Lex ([hëch<qst>+:hech\ ])

LEXICON Preverb-Lex

kati<vaux>+:kati\       # "almost do"
kata<vaux>+:kata\       # "want to do"       FIXME: p3 is kota/kahta
shinki<vaux>+:shinki\   # "dislike/hate to"
alemi<vaux>+:{t}{w}alemi\  # "begin"
tala<vaux>+:{t}ala\     # "cannot"


LEXICON NounInfl-Anim

<n><aa><sg>:
<n><aa><pl>:>{y}{A}k


LEXICON NounInfl-Inan

<n><nn><sg>:
<n><nn><pl>:>{y}{A}

LEXICON VerbInflAI(3)

:{N}{e}>   >{½}          <p1><sg>
:ni\ >     >{½}          <p1><sg>        # Dir/LR
:n'>       >{½}          <p1><sg>        # Dir/LR
:{k}{e}>   >{½}          <p2><sg>
:ki\ >     >{½}          <p2><sg>        # Dir/LR
:k'>       >{½}          <p2><sg>        # Dir/LR
:          >{³}          <p3><sg>        # technically -w (?)
:{N}{e}>   >{½}>hena     <p1><incl><pl>  # technically -hm-nan (MP-1.pl)
:ni\ >     >{½}>hena     <p1><incl><pl>  # Dir/LR
:n'>       >{½}>hena     <p1><incl><pl>  # Dir/LR
:{k}{e}>   >{½}>hena     <p1><excl><pl>
:ki\ >     >{½}>hena     <p1><excl><pl>  # Dir/LR
:ki\ >     >{½}>hena     <p1><excl><pl>  # Dir/LR
:{k}{e}>   >{½}>hemo     <p2><pl>
:          >{³}>{y}{A}k  <p3><pl>        # -w-ak (3-pl.anim)

:          >tam          <opt><p1><pl>

LEXICON VerbInflTI(3)

:{N}{e}>   >{e}n          <p1><sg><o_sg>
:{N}{e}>   >{e}n>a        <p1><sg><o_pl>
:{k}{e}>   >{e}n          <p2><sg><o_sg>
:{k}{e}>   >{e}n>a        <p2><sg><o_pl>
:{w}       >{e}n>a        <p3><sg><o_sg>
:{w}       >{e}n>a        <p3><sg><o_pl>
:{N}{e}>   >{e}nen        <p1><incl><pl><o_sg>
:{N}{e}>   >{e}nen>a      <p1><incl><pl><o_pl>
:{k}{e}>   >{e}nen        <p1><excl><pl><o_sg>
:{k}{e}>   >{e}nen>a      <p1><excl><pl><o_pl>
:{k}{e}>   >{e}n>eyo      <p2><pl>
:{w}       >{e}n>eyo      <p3><pl>
#:{k}{e}>   >en>eyo        <p2><pl><o_sg>
#:{k}{e}>   >en>eyo        <p2><pl><o_pl>
#:{w}       >en>eyo        <p3><pl><o_sg>
#:{w}       >en>eyo        <p3><pl><o_pl>

LEXICON VerbInflTA(3)

:{N}{e}>   >a               <dir><p1><sg><o_sg>
:{N}{e}>   >a>o>k           <dir><p1><sg><o_pl>
:{k}{e}>   >a               <dir><p2><sg><o_sg>
:{k}{e}>   >a>o>k           <dir><p2><sg><o_pl>
:{w}       >a>o             <dir><p3><sg><o_sg>
:{w}       >a>o             <dir><p3><sg><o_pl>           # Dir/LR
:{w}       >a>o>k           <dir><p3><sg><o_pl>
:{N}{e}>   >a>w>ena         <dir><p1><incl><pl><o_sg>
:{N}{e}>   >a>w>na          <dir><p1><incl><pl><o_sg>     # Dir/LR
:{N}{e}>   >a>w>enanak      <dir><p1><incl><pl><o_pl>
:{k}{e}>   >a>w>ena         <dir><p1><excl><pl><o_sg>
:{k}{e}>   >a>w>na          <dir><p1><excl><pl><o_sg>     # Dir/LR
:{k}{e}>   >a>w>enanak      <dir><p1><excl><pl><o_pl>
:{k}{e}>   >a>w>a           <dir><p2><pl><o_sg>
:{k}{e}>   >a>w>a           <dir><p2><pl><o_pl>
:{w}       >a>w>oo          <dir><p3><pl>
:{w}       >a>w>ao          <dir><p3><pl>                # Dir/LR


:{N}{e}>   >uk>w            <inv><p1><sg><s_sg>
:{N}{e}>   >kuk>w           <inv><p1><sg><s_pl>
:{k}{e}>   >uk>w            <inv><p2><sg><s_sg>
:{k}{e}>   >kuk>w           <inv><p2><sg><s_pl>
:{w}       >ku              <inv><p3><sg><s_sg>
:{w}       >ku              <inv><p3><sg><s_pl>
:{N}{e}>   >ku>na           <inv><p1><incl><pl><s_sg>
:{N}{e}>   >ku>nanak        <inv><p1><incl><pl><s_pl>
:{k}{e}>   >ku>na           <inv><p1><excl><pl><s_sg>
:{k}{e}>   >ku>nanak        <inv><p1><excl><pl><s_pl>
:{k}{e}>   >ku>w>a          <inv><p2><pl><s_sg>
:{k}{e}>   >ku>w>a>ok       <inv><p2><pl><s_pl>
:{w}       >ku>w>oo         <inv><p3><pl>


LEXICON N-Poss(3)

:{N}{e}>   :                <px1sg>
:{k}{e}>   :                <px2sg>
:          :>a              <px3sg>      ! Fix plurals  - technically -a is obviative
:{N}{e}>   :>ena            <px1plincl>
:{k}{e}>   :>ena            <px1plexcl>


PATTERN DetRoot

Determiners(1) [ <det>: ] Determiners(2):

PATTERN PronRoot

Pronouns(1) [ <prn>: ] Pronouns(2):




###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

LEXICON NounRoot-Anim

ahas:ahas               # "crow" pl - ahasak
tëme:tëme               # "wolf" pl - temeyok
maxkw:maxkw             # "bear" pl - maxkok
aiham:aiham             # "golden eagle" pl - aihamuk
ketukw:ket{u}kw         # "knee" pl - ketkuk
chahkol:chahkol         # "frog"
chulens:chulens         # "bird"
mwekane:mwekane         # "dog"
xanikw:xanikw           # "squirrel"
chinkwe:chinkwe         # "bobcat"
shkakw:shkakw           # "skunk"
opalaniye:opalaniye     # "bald eagle"
maxkalaniyat:maxkalaniyat # "hawk"
awèn:awèn               # "person" (pl: awènik)
hitukw:hit{u}kw         # "tree"
xkwe:xkwe               # "woman"
xkwechëch:xkwechëch     # "girl"
pilaechëch:pilaechëch   # "boy"
utènay:utènay           # "town"


LEXICON NounRoot-Inan

hempes:hemp{e}s			# "shirt, clothes" pl - hempsa
ahkwiyàn:ahkwiyàn			# "blanket"
chipakw:chipakw			# "shoe" pl - chipako
shope:shope					# "shore" pl - shopeyo
milaxk:milaxk				# "human hair"
mixeken:mixeken			# "body hair, fur"
mamon:mamon					# "eyebrow"
xkwekankan:xkewkankan	# "neck"
wananu:wananu				# "cheek" pl - wananuwa
hwikwi:hwikwi				# "chin"
shetun:shetun				# "lip"
wipit:wipit					# "tooth"
kwitsit:kwitsit			# "toe"
wiskon:wiskon				# "elbow"
hwikash:hwikash			# "nail, hoof, claw"
welench:welench			# "finger"
ahshilunkon:ahshilunkon	# "armpit"
tukwelench:tukwelench	# "fish"
xkelixen:xkelixen			# "rib"
xkan:xkan					# "bone"
mutay:mutay					# "stomach"
weshkinkw:weshkinkw		# "eye; face" pl - weshkinko
teh:teh						# "heart"
pom:pom						# "thigh; ham"
hwikxkon:hwikxkon			# "shin"
tankat:tankat				# "ankle"
tuhwepi:tuhwepi			# "body"
wil:wil						# "head"
hopikon:hopikon			# "shoulder"
hwitaok:hwitaok			# "ear"
tun:tun						# "mouth"
wikiyon:wikiyon			# "nose"
naolk:naolk					# "arm"
naxk:naxk					# "hand"
hwikat:hwikat				# "leg"
wsit:wsit					# "foot"
mehemichink:mehemichink # "food"
shakukwiyan:shakukwiyan # "coat"
lenhaksen:lenhaksen     # "moccasin"
mayakhwikan:mayakhwikan # "rake"
hòkes:hokes             # "bark; box"
mënëpekw:mënëpekw       # "lake"
muxul:muxul             # "canoe, boat"
tënte-tëpchehëlas:tënte-tëpchehëlas # "train" (CHECK - inan)
ahchu:ahchu{h}{w}       # "hill, (mountain)"
paent:paent             # "cup"
chipakw:chipakw         # "shoe"
ahshikan:ahshikan       # "sock"
lokens:lokens           # "plate"
ehukwenk:ehukwenk       # "necklace"
tepinxkepi:tepinxkepi   # "bracelet"
mokel:mokel             # "hammer"
kishku:kishku           # "day"  (pl: kishko)
xalahputisikaon:xalahputisikaon  # "spider web"
lehikan:lehikan         # "book; letter"

LEXICON NounRoot-Family(2)

# Vocative          Possessive stem
ana:ana             kahes:kahes         # "mother"
nuxa:nuxa           ux:ux             # "father"
nkwis:nkwis         kwis:kwis         # "son"
nichan:nichan       ichan:ichan       # "daughter"
uma:uma             uhem:uhem           # "grandmother"
mëxumsa:mëxumsa     mëxumes:mëxumes     # "grandfather"
xatash:xatash       mis:mis             # "older sister"
xansa:xansa         xans:xans           # "older brother"
naxisemes:naxisemes xisemes:xisemes # "younger sister/brother/cousin"


LEXICON VerbRootAI(2)

# Lesson 1
lamals:{w}lamals{IU}    <iv><aa> # "feel well"
tixëm:tixëm{w}{IU}      <iv><aa> # "bathe"
tixëm:tixàm{w}{IU}      <iv><aa> # "bathe"   ! Dir/LR
alam:{t}alam{IU}        <iv><aa> # "be frightened/scared" "корк"
wishas:wishas{IU}       <iv><aa> # "be frightened/startled" "чочу"
mikentam:mikentam       <iv><aa> # "do something"
pils:pils{IU}           <iv><aa> # "be clean"

# Lesson 2
pals:pals{IU}				<iv><aa>	# "be sick"
wikwihel:wikwihel{Æ}		<iv><aa>	# "be tired"
kes:kes{IU}					<iv><aa>	# "be hot"
akohch:{t}akohch{IU}		<iv><aa>	# "be cold"
alemsk:{t}alemsk{Æ}		<iv><aa>	# "leave/go away"
machi:mach{I}				<iv><aa>	# "go home"

# Lesson 3
asu:{t}asu{w}{IU}       <iv><aa> # "sing"
puhenema:puhenema       <iv><aa> # "drum"
kentk:kentk{Æ}          <iv><aa> # "dance"
lashim:{t}{e}lashim{w}{IU} <iv><aa> # "dream"

# Lesson 4
katup:katup{w}{IU}      <iv><aa> # "be hungry"
ite:{l}ite{h}e          <iv><aa> # "think"
mits:mits{IU}           <iv><aa> # "eat"
mëne:mëne               <iv><aa> # "drink"
katusëm:katusëm{w}{IU}  <iv><aa> # "be thirsty"

# Lesson 6
ashewil:ashewil         <iv><aa> # "swim"
pap:pap{IU}             <iv><aa> # "play"

# Lesson 7
këlëks:këlëks{IU}       <iv><aa> # "laugh"
melimu:melimu           <iv><aa> # "cry"
hakihe:{TH}akihe        <iv><aa> # "plant"
alakihel:{t}alakihel{Æ} <iv><aa> # "jump around"
winewe:winewe           <iv><aa> # "beg/ask"

# 
kenthw:kenthw{IU}       <iv><aa> # "fly"
pus:pus{IU}             <iv><aa> # "get into a boat"
shielintam:shielintam   <iv><aa> # "be sad"
lelintam:{W}{e}lelintam <iv><aa> # "be glad"   ! FIXME

# Lesson 10

a:{Æ}                   <iv><aa> # "go"
pa:p{Æ}                 <iv><aa> # "come"
welinakws:welinakws     <iv><aa> # "look good"
um:um                   <iv><aa> # "come from"
watu:watu               <iv><aa> # "know something"

# Lesson 18

mikëmòs:mikëmòs{IU}     <iv><aa> # "work"
chikhike:chikhike       <iv><aa> # "sweep"
shilenche:shilenche     <iv><aa> # "wash hands"
wesks:wesks{IU}         <iv><aa> # "be young"
shixënchuwe:shixënchuwe <iv><aa> # "wash dishes"
shixukwe:shixukwe       <iv><aa> # "wash one's hair"
shixhems:shixhemps{Æ}   <iv><aa> # "wash clothes"


# Lesson 20

alai:{t}alai            <iv><aa> # "hunt"
wik:wik{IU}             <iv><aa> # "to dwell"
weskahp:weskahp{IU}     <iv><aa> # "be born"
ahchinkx:ahchinkx{IE}   <iv><aa> # "be stubborn/disobedient"

# Lesson 21

kelixike:kelixike       <iv><aa> # "sew"
ahponhe:ahponhe         <iv><aa> # "make bread"
patam:patam             <iv><aa> # "pray"
manaxe:manaxe           <iv><aa> # "cut/go after wood"

# Lesson 22

ame:ame                 <iv><aa> # "fish" - no prefixes! (ame, ame, ame, amehena, amehena, amehemo, ameyok)
penhatu:penhatu         <iv><aa> # "leave tracks"
tkauxwe:tkauxwe         <iv><aa> # "walk slowly"


LEXICON VerbRootTI(2)

tankhitu:t{A}nkhitu     <tv><nn> # "lose"
maxkam:m{A}xkam         <tv><nn> # "find"
kaxksem:k{A}xksem       <tv><nn> # "dry"

kwetkenem:kwetkenem     <tv><nn> # "return it"
kelenem:k{w}elenem      <tv><nn> # "carry/hold it"

# Lesson 10
watun:watun             <tv><nn> # "know/know about it"
lamhitam:lamhitam       <tv><nn> # "believe it"
tatun:tatun             <tv><nn> # "put/place it"

# Lesson 20

meshatam:m{w}eshatam    <tv><nn> # "remember it"

# Lesson 21

kelixham:kelixham       <tv><nn> # "sew it"
kelixham:kelixam        <tv><nn> # "sew it"  # Dir/LR



LEXICON VerbRootTA(2)

mil:mil                 <tv><aa> # "give"
new:new                 <tv><aa> # "see"

# Lesson 18
wichëm:wichëm           <tv><aa> # "help someone"

# Lesson 20
nachih:nachih           <tv><aa> # "bother someone"

# Lesson 21
kelen:kelen             <tv><aa> # "carry/hold someone"

# Lesson 22

wixen:wixen             <tv><aa> # "cook something animate"
naol:naol               <tv><aa> # "follow"



LEXICON AdjLex

sek:sek                # be black     seksu|seksuwak (an) / seke|sekeyo (inan)
wisaw:wisaw            # be yellow    wisawsu|wisawsuwak / wisae|wisaeyo
òp:òp                  # be white     opsu|opsuwak / ope~opek|opeyoá
àskàskw:àskàskw        # be green
aon:aon                # be blue      aonsu / aone
wipunkw:wipunkw        # be grey
màxk:màxk              # be red
likt:likt              # be a/its colour  liksu / likte
siskwinakw:siskwinakw  # be brown
wipunkw:wipunkw        # be grey
mamal:mamal            # be striped
sësàp:sësàp            # be spotted
wël:wël                # be pretty

LEXICON AdjLex-Cooking

salà:salà              # be fried
wën:wën                # be boiled
lu:lu                  # be burned


LEXICON Determiners(2)

në:në				 <dem><dist><nn><sg>
na:na				 <dem><dist><aa><sg>
nel:nel			 <dem><dist><nn><pl>
nek:nek         <dem><dist><aa><pl>
nek:neki        <dem><dist><aa><pl>   # Dir/LR
yu:yu           <dem><prox><nn><sg>   # "this (inan)"
wa:wa           <dem><prox><aa><sg>   # "this (anim)"
yuli:yuli       <dem><prox><nn><pl>
yuki:yuki       <dem><prox><aa><pl>

nel:nel			 <dem><dist><obv><an><sp>  # sg/pl an/in obviative

LEXICON Pronouns(2)

nën:nën         <dem><dist><nn><sg>
nàn:nàn         <dem><dist><aa><sg>
awèn:awèn       <itg>                   # "who"
kèku:kèku       <itg>                   # "what"
#nëpe:nëpe                               # "me too"
#këpe:këpe                               # "you too"

LEXICON AdvLex

yukwe:yukwe       # "now"
lòkëwe:lòkëwe     # "yesterday"
she:she           # "here"
ika:ika           # "there"
wëlèh:wëlèh       # "there; over there"
konaet:konaet     # "maybe"
xuniti:xuniti     # "soon"
na:na             # "then"

kahti:kahti       # "almost"
sòmi:sòmi         # "very"

xu:xu             # [future]
mechi:mechi       # "already" / [perfect]
xàntki:xàntki     # "finally"
lekhikwi:lekhikwi # "at this time"
tamse:tamse       # "sometimes"
àpchi:àpchi       # "always"

lòmëwe:lòmëwe     # "long ago"
kèxiti:kèxiti     # "a little; a little bit"

kishkwik:kishkwik # "today"

wëli:wëli         # "very; good; well"

kwiakwi:kwiakwi   # "more; still"
nahi:nahi         # "downstream" (final stress)


LEXICON CnjLex(2)

òk:òk       <cnjcoo>      # "and"
èli:èli     <cnjsub>      # "because"
tili:tili   <cnjsub>      # "that"

LEXICON Prepositions

wichi:wichi     # "with"
hapi:hapi       # "with"
naxpi:naxpi     # "with"
wénchi:wénchi

LEXICON NumberLex

kwëti:kwëti           # "one"
nisha:nisha           # "two"
naxa:naxa             # "three"
newa:newa             # "four"
palenaxk:palenaxk     # "five"
kwetash:kwetash       # "six"
nishash:nishash       # "seven"
xash:xash             # "eight"
peshkunk:peshkunk     # "nine"
telen:telen           # "ten"
nishinxke:nishinxke   # "twenty"
xinxke:xinxke         # "thirty"
neinxke:neinxke       # "forty"
palenaxk\ txintxke:palenaxk\ txintxke  # "fifty"  FIXME: CHECK
kwetash\ txinxke:kwetash\ txinxke      # "sixty"
nishash\ txinxke:nishash\ txinxke      # "seventy"
xash\ txinxke:xash\ txinxke            # "eighty"
peshkunk\ txinxke:peshkunk\ txinxke    # "ninety"
kwetapxki:kwetapxki                    # "one hundred"
nishapxki:nishapxki                    # "two hundred"
naxapxki:naxapxki                      # "three hundred"
neopxki:neopxki                        # "four hundred"
palenaxk\ txapxki:palenaxk\ txapxki    # "five hundred"
kwetash\ txapxki:kwetash\ txapxki      # "six hundred"
nishash\ txapxki:nishash\ txapxki      # "seven hundred"
xash\ txapxki:xash\ txapxki            # "eight hundred"
peshkunk\ txapxki:peshkunk\ txapxki    # "nine hundred"
telen\ txen\ kwetapxki:telen\ txen\ kwetapxki    # "one thousand"


LEXICON IjLex

yuh:yuh         # "alright, okay"

LEXICON Punctuation

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:? 
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# http://wiki.apertium.org/wiki/Hfst

