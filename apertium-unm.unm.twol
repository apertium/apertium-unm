Alphabet
 A B C D E F G H I J K L M N O P Q R S T U V W Y Z
 a b c d e f g h i j k l m n o p q r s t u v w y z
 %{e%}:0 %{e%}:e
 %{u%}:0 %{u%}:u
 %{y%}:0 %{y%}:y %{y%}:w
 %{A%}:a %{A%}:o %{A%}:u
 %>:0  ;

Sets

Vows = A E I O U
       a e i o u ;

Cons = B C D F G H J K L M N P Q R S T V W Y Z
       b c d f g h j k l m n p q r s t v w y z ;

Stop = P T K
       p t k ;

Rules

"Epenthetic vowels surface"
Vx:Vy <=> _ [ :Cons :Cons ]/:0* ;
          _ :Cons/:0* .#. ;
      except
            _ [ [ :c :h ] | [ :Stop :w ] ]/:0* ;
      where Vx in ( %{e%} %{u%} )
            Vy in (   e     u   )
      matched ;

"{y} as y after e"
%{y%}:y <=> :e/:0* _ ;

"{y} as w after u"
%{y%}:w <=> :u/:0* _ ;

!"non-a values of {A}"
!%{A%}:Vx <=> [ Sx: %{y%}: ]/[ :0 - %{y%}: ]* _ ;
!         where
!             Sx in ( e w m )
!             Vx in ( o o u )
!         matched ;

"{A} as o after kw or eC"
%{A%}:o <=> [ e: %{y%}: ]/[ :0 - %{y%}: ]* _ ;
            [ k: w: %{y%}: ]/[ :0 - %{y%}: ]* _ ;

"{A} as u after m"
%{A%}:u <=> m:/[ :0 - %{y%}: ]* _ ;

"w{A} coalesces to o"
w:0 <=> Cons:/:0* _ %{A%}:/:0* ;


! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

