Alphabet
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z À È Ë Ì Ò Ù
 a b c d e f g h i j k l m n o p q r s t u v w x y z à è ë ì ò ù
 %{e%}:0 %{e%}:ë
 %{u%}:0 %{u%}:u
 %{ù%}:0 %{ù%}:ù
 %{y%}:0 %{y%}:y %{y%}:w
 %{A%}:à %{A%}:ò %{A%}:ù
 %{k%}:k %{k%}:0
 %{N%}:n %{N%}:m %{N%}:0
 %{n%}:n %{n%}:0
 %{t%}:t %{t%}:0
 %{TH%}:t %{TH%}:h
 %{IU%}:i %{IU%}:u
 %{Æ%}:a %{Æ%}:e %{Æ%}:ò
 %{w%}:w %{w%}:0  ! don't appear by u
 %{ʍ%}:w %{ʍ%}:0  ! for negative behaviour; reverse (appear after u)
 %{%_w%}:w %{%_w%}:0  ! space holder for metathesised 3rd person w
 %{W%}:w %{W%}:u
 %{Y%}:w %{Y%}:y
 %{I%}:i
 %{ì%}:0 %{ì%}:ì
 %{i%}:0 %{i%}:i
 %{K%}:k %{K%}:t
 %{½%}:0
 %{³%}:0  ! triggers p3 root-initial and root-final alternations
 %{₃%}:0  ! triggers only p3 root-initial alternations (for subordinate)
 %>:0  ;

Sets

Vows = A E I O U  À È Ë Ì Ò Ù
       a e i o u  à è ë ì ò ù ;

Cons = B C D F G H J K L M N P Q R S T V W X Y Z
       b c d f g h j k l m n p q r s t v w x y z ;

Stop = P T K
       p t k ;

EpentheticVs = %{ë%} %{u%} %{ù%} ;

Pers = %{½%} %{³%} ;

ConsWithNoW = %{t%} t ;

Rules

"Epenthetic vowels surface"
Vx:Vy <=> :Cons _ [ :Cons :Cons ]/[ :0 - [ EpentheticVs: ] ]* ;
          :Cons _ :Cons/[ :0 - [ EpentheticVs: ] ]* .#. ;  ! hitukw
          [ %{N%}: | %{k%}: ] _ (%>:) m: ;
          %{N%}: _ (%>:) w: ;
          %{k%}: _ (%>:) [ p: | n: | w: ] ;
          .#. %{w%}:w _ %>: :Cons ;
          %{e%}: (%>:) %{t%}: _ :Cons ;
			 :Cons/[ :0 - [ %{e%}: | %{u%}: ] ]* _ n: ;
          .#. [ :Cons | :0 ]* %{t%}:t _ :l ;  ! tëlao
      ! except
      !      _ [ [ :c :h ] | [ :Stop :w ] ]/:0* ;    ! this makes hitukw fail; what was it for?
      where Vx in ( %{e%} %{u%} %{ù%} )
            Vy in (   ë     u     ù   )
      matched ;

"ëwë as u"
%{e%}:u <=> .#. :Cons _ (%>:) %{w%}: %{e%}: :Cons ;

"{y} as y after e and i"
%{y%}:y <=> [ :e | :i ]/:0* _ ;

"{y} as w after u"
%{y%}:w <=> :u/:0* _ ;

!"non-a values of {A}"
!%{A%}:Vx <=> [ Sx: %{y%}: ]/[ :0 - %{y%}: ]* _ ;
!         where
!             Sx in ( e w m )
!             Vx in ( o o u )
!         matched ;

"{A} as o after kw or eC|iC or a"
%{A%}:ò <=> [ [ :e | :i ] %{y%}: ]/[ :0 - %{y%}: ]* _ ;
            [ k: w: %{y%}: ]/[ :0 - %{y%}: ]* _ ;
            :a/[ :0 ]* _ ;
            %{Y%}:/[ :0 ]* _ ;

"{A} as u after m"
%{A%}:ù <=> m:/[ :0 - %{y%}: ]* _ ;

"w{A} coalesces to o"
w:0 <=> Cons:/:0* _ %{A%}:/:0* ;

"{IU} as i in p1 and p2, u in p3"
%{IU%}:Vx <=> _ [ :0 - Pers: ]* Px: ;
          except
              :u %{w%}: _ [ :0 - %{y%}: ]* %{y%}: ;
          where
              Vx in (   i     u   )
              Px in ( %{½%} %{³%} )
          matched ;

"{IU} deletes if between u{w} and {y}"
%{IU%}:0 <=> :u %{w%}: _ [ :0 - %{y%}: ]* %{y%}: ;

"{Æ} as a in p1 and p2, e in p3"
%{Æ%}:Vx <=> _ [ :0 - Pers: ]* Px: ;
          where
              Vx in (   a     e   )
              Px in ( %{½%} %{³%} )
          matched ;

"{t} deletes when word initial etc."
%{t%}:0 <=> .#. _ ;                      ! but not after {w}>
!            %{k%}: %{e%}: %>: _ %{e%}: ;

"{TH} as h when word initial"
%{TH%}:h <=> .#. _ ;

"{k} deletes before k"
%{k%}:0 <=> _ %{e%}:/[ :0 - %{e%}: ]* k: ;

"{N} and deletes before n"
%{N%}:0 <=> _ %{e%}:/:0* n: ;

"{N} as m before p"
%{N%}:m <=> _ :0* p: ;

"{N} as 0 before n"
%{N%}:0 <=> _ :0* n: ;

"{w} as 0 if followed by u or preceded by u or preceding certain consonants"
%{w%}:0 <=> _ %{e%}:? :0* %{IU%}:u ;
            _ %{e%}:? :0* [ m: ]  ;  ! | n:
            :u :0* _ ;
            _ %{e%}:? :0* ConsWithNoW: ;
            _ %{e%}:? :Cons/[ :0 - %{w%}: ]* %{%_w%}: ;

"realise {w} in metathesised position"
%{%_w%}:w <=> %{w%}: :Cons/[ :0 - %{%_w%}: ]* _ ;

"{w} as u"
%{W%}:u <=> %{e%}: %>: _ %{e%}: ;

!"{e} as 0"
!%{e%}:0 <=> %{k%}: _ %>: w: ;
!            %{N%}: _ %>: p: ;

"{n} as n only after a- and a/e-final form"
%{n%}:n <=> [ %{Æ%}: | a: ] %>: %{½%}: %>: a: _ ;

"{u}:u only after l, m, n"
%{u%}:u <=> [ :l | :m | :n ] [ :0 - %{u%}: ]* _ ;

"{ù}:ù only after l, m, n"
%{ù%}:ù <=> [ :l | :m | :n ] [ :0 - %{ù%}: ]* _ ;



"{ʍ}:w only after {u}:u"
%{ʍ%}:w <=> %{u%}:u _ ;

"{K} as k only after n"
%{K%}:k <=> :n :0* _ ;

"Y as w before a"       ! FIXME: check how neyo/new- works
%{Y%}:w <=> _ :0* [ :a | :e ] ;

"a as o after {Y}"
a:o <=> %{Y%}: %>: _ ;

! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! https://wiki.apertium.org/wiki/Twol
! https://wiki.apertium.org/wiki/Hfst

